// This sets us up for building a forge project - you need all of these
buildscript {
    repositories {
        mavenCentral()
        maven {
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }
        maven {
            name = "sonatype"
            url = "https://oss.sonatype.org/content/repositories/snapshots/"
        }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.1-SNAPSHOT'
    }
}
plugins {
    id "com.matthewprenger.cursegradle" version "1.0.7"
}


// Apply the forge plugin - this adds all the magic for automatically obfuscating, deobfuscating etc
apply plugin: 'net.minecraftforge.gradle.forge'
apply plugin: 'maven'
apply plugin: 'eclipse'
apply plugin: 'idea'

// This is a simple flatdir repository for "uploadArchives" when you don't have a remote repo to target.
repositories {
    flatDir {
        name "fileRepo"
        dirs "repo"
    }
    maven {
        name "IC2"
        url "http://maven.ic2.player.to/"
    }
}
// CompactSolars uses git tagging to mark major versions. This sets up the project version to that version data
def versionInfo = getGitVersion()
version = "${project.minecraft.version}-${versionInfo['CompactSolars.version']}"
curseforge {
    apiKey = project.hasProperty('curseforge_apikey') ? project.curseforge_apikey : '0'
    project {
        id = '228759'
        changelog = 'Empty'
        releaseType = 'beta'
    }
}


// This is our group. I'm cpw.mods
group= "cpw.mods" // http://maven.apache.org/guides/mini/guide-naming-conventions.html
// This is our actual project within the group. Note: FML has "fml" here. But this is CompactSolars.
archivesBaseName = "CompactSolars"

// Setup the forge minecraft plugin data. Specify the preferred forge/minecraft version here
minecraft {
    version = "1.8.9-11.15.1.1855"
    mappings = "stable_22"
    runDir = "run"
}

dependencies {
    compile 'net.industrial-craft:industrialcraft-2:2.3.262-ex18:dev'
    //compile fileTree(dir: 'libs', include: ['*.jar'])
}

// This wrangles the resources for the jar files- stuff like textures and languages
processResources
{
    // we're omitting the .xcf files - they're development only
	exclude '**/*.xcf'
    // we only want to do search/replace stuff in mcmod.info, nothing else
    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'
                
        // replace version and mcversion
        expand 'version':project.version, 'mcversion':project.minecraft.version
    }
        
    // copy everything else, thats not the mcmod.info
    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }

	// generate version.properties file from the git version data earlier
	doLast {
		def propsFile = new File(destinationDir, 'version.properties')
		def properties = new Properties()
		properties.putAll(versionInfo)
		properties['CompactSolars.build.mcversion'] = project.minecraft.version
		properties.store(propsFile.newWriter(), null)
	}
}

// this sets our output jar to have a 'tag' of 'universal' on it
// It also adds the minecraft version in a custom version name
// The result is files named <projectname>-<mcversion>-<version>-universal.jar
jar {
    classifier = 'universal'
}

println "FISHBUM ${jar.version}"

// Configure an upload task. this is setup for uploading to files.minecraftforge.net. There are other examples around
uploadArchives {
    repositories.mavenDeployer {

        dependsOn 'build'

        if (project.hasProperty('forgeMavenPassword'))
        {
            repository(url: "http://files.minecraftforge.net/maven/manage/upload") {
                authentication(userName: project.getProperty('forgeMavenUser'), password: project.getProperty('forgeMavenPassword')) // the elvis operator. look it up.
            }
        }
        else
        {
            // local repo folder. Might wanna juset use  gradle install   if you wanans end it to maven-local
            repository(url: 'file://localhost/' + project.file('repo').getAbsolutePath())
        }


        // This is just the pom data for the maven repo
        pom {
            groupId = project.group
            // Force the maven upload to use the <mcversion>-<version> syntax preferred at files
            version = "${project.version}"
            artifactId = project.archivesBaseName
            project {
                name project.archivesBaseName
                packaging 'jar'
                description 'CompactSolars'
                url 'https://github.com/cpw/CompactSolars'

                scm {
                    url 'https://github.com/cpw/CompactSolars'
                    connection 'scm:git:git://github.com/progwml6/CompactSolars.git'
                    developerConnection 'scm:git:git@github.com:progwml6/CompactSolars.git'
                }
                issueManagement {
                    system 'github'
                    url 'https://github.com/progwml6/CompactSolars/issues'
                }

                licenses {
                    license {
                        name 'GNU Public License (GPL), Version 3.0'
                        url 'http://www.gnu.org/licenses/gpl-3.0.txt'
                        distribution 'repo'                        
                    }
                }

                developers {
                    developer {
                        id 'cpw'
                        name 'cpw'
                        roles { role 'developer' }
                    }
                }
            }
        }
    }
}

// This is a special task for pulling the version information from git and the environment (for BUILD_NUMBER)
def getGitVersion()
{
	def out = [:]

	// call git command.
	def outStream = new ByteArrayOutputStream()
	def result = exec {
		executable = 'git'
		args = [ 'describe', '--long', "--match=[^(jenkins)]*"]
		standardOutput = outStream
	}

	def fullVersion = outStream.toString().trim()
	def matcher = fullVersion =~ /(\d+).(\d+)-(\d+)-(.*)/

	def maj = matcher[0][1]
	def min = matcher[0][2]
	def rev = matcher[0][3]
	def bn = System.getenv("BUILD_NUMBER") ?: "1"

	out['CompactSolars.build.major.number'] = maj.toString()
	out['CompactSolars.build.minor.number'] = min.toString()
	out['CompactSolars.build.revision.number'] = rev.toString()
	out['CompactSolars.build.githash'] = matcher[0][4].toString()
	out['CompactSolars.build.number' ] = bn.toString()
	out['CompactSolars.version' ] = "${maj}.${min}.${rev}.${bn}".toString()

	return out
}
